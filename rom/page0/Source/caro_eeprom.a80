; процедуры работы с EEPROM в контроллере клавиатуры by Caro

; Чтение байта из ячейки EEPROM
; Вход: H -> ADR_eeprom
; Выход: L = (DAT_eeprom)
;	 CY = 0 - OK
rd_eeprom

	di
	call	set_adr	; Задать адрес EEPROM
	jr	c,eeprom_err	;Выход с ошибкой
; Задан адрес ячейки EEPROM в контроллере
	ld	a,0x55  ; код доступа
	in	a,(0xFE)
	ld	a,"R"	;Команда чтения EEPROM
	in	a,(0xFE)
; Контроллер ждет чтения двух половинок байта
	in	a,(0xFE)
	rlca
	rlca
	rlca
	rlca
	and	0xF0	;ст. нибл данных
	ld	l,a	;сохранить
	in	a,(0xFE)
	and	0x0F	;мл. нибл данных
	or	l
	ld	l,a	; байт данных из EEPROM
eeprom_err		;выход - разрешаем прерывания
	ei
	ret		; CY=0
;-------------------------------------------
; Запись байта в EEPROM
; Вход: H -> ADR_eeprom
;        L -> (DAT_eeprom)
;	 CY = 0 - OK
wr_eeprom
	di
	call	set_adr	; Задать адрес EEPROM
	jr	c,eeprom_err	;  Выход с ошибкой
; Задан адрес ячейки EEPROM в контроллере
	ld	a,0x55  ; код доступа
	in	a,(0xFE)
	ld	a,"W"	; Команда записи в EEPROM
	in	a,(0xFE)
	ld	a,l	; Байт данных
	in	a,(0xFE);  Записать
	ei
	ret		; CY=0
;-------------------------------------------
; Задать текущий адрес ячейки EEPROM.
; После операций чтения/записи
; происходит авто-инкремент текущего адреса.
set_adr	ld	a,0x55	;Код доступа
	in	a,(0xFE) ; байт ответа
	and	0x1F     ; маска
	cp	0x0A	; должен прочитаться 0Ah
	scf		; CY=1 
	ret	nz	; Иначе выход с ошибкой
; Контроллер откликнулся
	ld	a,"A"	;Команда задания адреса
	in	a,(0xFE)
	ld	a,h	;Мл.Байт адреса
	in	a,(0xFE)
;	ld	a,H	;Ст.Байт адреса
	xor	a	; Т.к. оперируем только 256 ячейками, то старший адрес = 0
	in	a,(0xFE)
	xor	a	; CY=0
	ret		; Выход без ошибки
;==============================================
